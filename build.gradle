// Root project build script with centralized dependency management
plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.3' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

ext {
    // Global version properties
    javaVersion = JavaLanguageVersion.of(21)
    springBootVersion = '4.0.3'
    springDependencyManagementVersion = '1.1.7'
    lombokVersion = '1.18.30'
    junitVersion = '5.10.1'
}

group = 'org.s3m'
version = '0.0.1-SNAPSHOT'
description = 'FamilyBudget'

subprojects {
    apply plugin: 'java'

    group = rootProject.group
    version = rootProject.version

    java {
        toolchain {
            languageVersion = rootProject.ext.javaVersion
        }
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${rootProject.ext.lombokVersion}"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

// Apply Spring Boot configurations only to service modules
configure(subprojects.findAll { it.name in ['fb-userService', 'fb-chatService'] }) {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${rootProject.ext.springBootVersion}"
        }
    }
}

